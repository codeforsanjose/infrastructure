module "application_container_def" {
  source            = "cloudposse/ecs-container-definition/aws"
  version           = "0.56.0"

  container_name    = local.container_name
  container_image   = "redislabs/rejson:latest"
  container_cpu               = var.container_cpu
  container_memory_reservation = 128
  port_mappings = [
    {
      containerPort = 6379
      hostPort      = 6379
      protocol      = "tcp"
    }
  ]
  log_configuration = {
    logDriver = "awslogs"
    options = {
      awslogs-group         = format("ecs/%s", local.ecs_service_name)
      awslogs-region        = var.region
      awslogs-stream-prefix = "redis"
    }
  }
}

resource "aws_ecs_task_definition" "task" {
  family = local.task_definition_family

  container_definitions    = module.application_container_def.json_map_encoded_list
  requires_compatibilities = ["EC2"]
  network_mode             = "awsvpc"
  memory                   = var.container_memory == 0 ? null : var.container_memory
  cpu                      = var.container_cpu == 0 ? null : var.container_cpu
  tags                     = var.tags
}

resource "aws_ecs_service" "svc" {
  name            = local.ecs_service_name
  cluster         = var.cluster_id
  task_definition = aws_ecs_task_definition.task.arn
  launch_type     = "EC2"
  desired_count   = var.desired_count
  tags            = var.tags

  service_registries {
    registry_arn = aws_service_discovery_service.sd_service.arn
  }

  network_configuration {
    subnets         = var.public_subnet_ids
    security_groups = [aws_security_group.redis_sg.id]
  }

  depends_on = [aws_service_discovery_service.sd_service]
}

resource "aws_security_group" "redis_sg" {
  name        = "${var.resource_name}-${var.environment}-redis"
  description = "inbound from load balancer to ecs service"

  vpc_id = var.vpc_id

  ingress {
    description = "Allow all inbound redis connection - may change to only internal vpc only"
    from_port   = 6379
    to_port     = 6379
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    description = "outbound traffic to the world"
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = merge({ Name = format("%s-redis", "${var.resource_name}-${var.environment}") }, var.tags)
}

// Create group for streaming application logs
resource "aws_cloudwatch_log_group" "cwlogs" {
  name              = format("ecs/%s", local.ecs_service_name)
  retention_in_days = 14
}
